mapscripts PewterMineNorthHouse_MapScripts{
	MAP_SCRIPT_ON_LOAD: PewterMineNorthHouse_OnLoad
}
	
script PewterMineNorthHouse_OnLoad{
	if(var(VAR_STORY_PROGRESS)>14){
	setflag(FLAG_TEMP_3)
	}
	end
}

script PewterMineNorthHouse_Morgan{
	lock
		if(var(VAR_STORY_PROGRESS)==14){
		getplayerxy(VAR_RESULT,VAR_TEMP_1)
		playse(SE_PIN)
		applymovement(1,PewterMine_Exclam)
		waitmovement(1)
		msgbox("Ah!")
		if(var(VAR_RESULT)==7){
		applymovement(OBJ_EVENT_ID_PLAYER,PewterMineNorth_PlayerStep)
		}
		applymovement(1,PewterMineNorthHouse_MorganSouth)
		waitmovement(1)
		applymovement(1,PewterMineNorthHouse_MorganSouth)
		waitmovement(1)
		msgbox(format("Are you... {PLAYER}?\nSo Oak must have sent you."))
		applymovement(1,PewterMineNorthHouse_MorganSide)
		waitmovement(1)
		msgbox(format("I'm Morgan, his assistant.\nAs you might have guessed, I got trapped in here when the fighting started. And just my luck, I had left my Pokémon at the Pokémon Center to rest..."))
		msgbox(format("Incidentally, that sound just now..."))
		waitmessage
		delay(30)
		applymovement(1,PewterMine_Exclam)
		waitmovement(1)
		msgbox(format("An explosion? Really!?"))
		msgbox(format("I need to get out of here. And you should too, {PLAYER}. We don't know how badly the mine tunnels were damaged..."))
		applymovement(1,PewterMineNorthHouse_MorganThree)
		waitmovement(1)
		playse(SE_PIN)
		applymovement(1,PewterMine_Exclam)
		waitmovement(1)
		msgbox(format("Oh! And if you have time, come back to the lab and tell Oak what you saw. He'll want to hear it from your perspective, too."))
		applymovement(1,PewterMineNorthHouse_MorganSouth)
		waitmovement(1)
		playse(SE_DOOR)
		setflag(FLAG_TEMP_3)
		removeobject(1)
		addvar(VAR_STORY_PROGRESS,1)
	}
release
end
}



movement PewterMineNorthHouse_MorganSouth{
walk_down
}
movement PewterMineNorthHouse_MorganSide{
face_left
delay_16
face_down
}
movement PewterMineNorthHouse_MorganThree{
walk_left
walk_down
walk_down
face_right
}
movement PewterMineNorth_PlayerStep{
walk_right
face_up
}